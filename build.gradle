plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

group = 'ink.wulian.er'
sourceCompatibility = '17'
version = '1.0.0'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    // lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'

    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco")
    }
}

java {
    withSourcesJar()
}

publishing {

    repositories {
        maven {
            url 'https://nexus.5lian.ink/repository/the-lands-between/'
            credentials {
                username 'dev-0'
                password 'todo'
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Godrick'
                description = 'Web Kits Collections.'
                url = 'https://nexus.5lian.ink'
            }
        }
    }

}

